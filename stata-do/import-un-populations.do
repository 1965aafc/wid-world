// Both sexes, all ages ----------------------------------------------------- //

import delimited "$un_data/populations/wpp/WPP2015_DB04_Population_By_Age_Annual.csv", ///
	delimit(",") case(lower) clear

keep if time<$year

// Identify countries
countrycode location, generate(iso) from("wpp")
drop location locid varid variant midperiod sexid

// Calculate population for 80+
replace pop_80_100 = pop_80_84 + pop_85_89 + pop_90_94 + pop_95_99 + pop_100 if mi(pop_80_100)
rename pop_80_100 pop_80

// Calculate other population categories
generate pop_children = pop_0_4 + pop_5_9 + pop_10_14 + pop_15_19
generate pop_adults = pop_20_24 + pop_25_29 + pop_30_34 + pop_35_39 + ///
	pop_40_44 + pop_45_49 + pop_50_54 + pop_55_59 + pop_60_64 + ///
	pop_65_69 + pop_70_74 + pop_75_79 + pop_80
generate pop_20_39 = pop_20_24 + pop_25_29 + pop_30_34 + pop_35_39
generate pop_40_59 = pop_40_44 + pop_45_49 + pop_50_54 + pop_55_59
generate pop_60 = pop_60_64 + pop_65_69 + pop_70_74 + pop_75_79 + pop_80
generate pop_20_64 = pop_20_24 + pop_25_29 + pop_30_34 + pop_35_39 + ///
	pop_40_44 + pop_45_49 + pop_50_54 + pop_55_59 + pop_60_64
generate pop_65 = pop_65_69 + pop_70_74 + pop_75_79 + pop_80
generate pop_20_29 = pop_20_24 + pop_25_29
generate pop_30_39 = pop_30_34 + pop_35_39
generate pop_40_49 = pop_40_44 + pop_45_49
generate pop_50_59 = pop_50_54 + pop_55_59
generate pop_60_69 = pop_60_64 + pop_65_69
generate pop_70_79 = pop_70_74 + pop_75_79
generate pop_80_89 = pop_80_84 + pop_85_89
generate pop_90_99 = pop_90_94 + pop_95_99

// Generate entire population
generate pop_all = pop_0_4 + pop_5_9 + pop_10_14 + pop_15_19 + pop_20_24 ///
	+ pop_25_29 + pop_30_34 + pop_35_39 + pop_40_44 + pop_45_49 + pop_50_54 ///
	+ pop_55_59 + pop_60_64 + pop_65_69 + pop_70_74 + pop_75_79 + pop_80

reshape long pop_, i(time sex iso) j(age) string
rename pop_ value
drop if mi(value)
replace value = 1e3*value

// One should have 38 age decompositions
qui tab age
assert `r(r)'==38

// Recode and rename
rename time year
replace sex="both" if sex=="Both"
replace sex="women" if sex=="Female"
replace sex="men" if sex=="Male"

label data "Generated by import-un-populations.do"
save "$work_data/un-population.dta", replace


