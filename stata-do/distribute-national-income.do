// Averages
use "$work_data/calculate-per-capita-series-output.dta", clear

drop currency

generate sixlet = substr(widcode, 1, 6)
generate pop = substr(widcode, 7, 3)
generate vartype = substr(widcode, 10, 1)

drop widcode

keep if p == "pall"
keep if inlist(sixlet, "anninc", "afiinc")

drop if pop == "999"
drop if vartype == "t"
drop vartype

reshape wide value, i(iso year p pop) j(sixlet) string

generate double factor = valueanninc/valueafiinc

drop valueanninc valueafiinc

drop if missing(factor)

tempfile factor
save "`factor'"

use "$work_data/calculate-per-capita-series-output.dta", clear

drop currency

generate sixlet = substr(widcode, 1, 6)
generate pop = substr(widcode, 7, 3)
generate vartype = substr(widcode, 10, 1)

keep if sixlet == "afiinc"

merge n:1 iso year using "`factor'", nogenerate keep(match)

replace value = value*factor
replace sixlet = "aptinc"
replace widcode = sixlet + pop + vartype

drop factor sixlet pop vartype

drop if inlist(iso, "CN", "FR", "US")

tempfile averages
save "`averages'"

// Shares
use "$work_data/calculate-per-capita-series-output.dta", clear

keep if substr(widcode, 1, 9) == "sfiinc992"
replace widcode = "sptinc992" + substr(widcode, 10, 1)

drop if inlist(iso, "CN", "FR", "US")

tempfile shares
save "`shares'"

// Aggregates
use "$work_data/calculate-per-capita-series-output.dta", clear

drop currency

generate sixlet = substr(widcode, 1, 6)
generate pop = substr(widcode, 7, 3)
generate vartype = substr(widcode, 10, 1)

keep if sixlet == "mfiinc"

merge n:1 iso year using "`factor'", nogenerate keep(match)

replace value = value*factor

replace widcode = "mptinc" + pop + vartype

drop factor sixlet pop vartype

drop if inlist(iso, "CN", "FR", "US")

tempfile aggregates
save "`aggregates'"

// Add to main file
use "$work_data/calculate-per-capita-series-output.dta", clear

merge 1:1 iso year p widcode using "`averages'", update nogenerate
merge 1:1 iso year p widcode using "`shares'", update nogenerate
merge 1:1 iso year p widcode using "`aggregates'", update nogenerate

label data "Generated by distribute-national-income.do"
save "$work_data/distribute-national-income-output.dta", replace

// Create metadata
use "`averages'", clear
append using "`shares'"
append using "`aggregates'"

keep iso widcode

generate sixlet = substr(widcode, 1, 6)
drop widcode
duplicates drop

generate method = "Fiscal income rescaled to match the macroeconomic aggregates."
generate source = "WID.world computations using fiscal and net national income."

tempfile meta
save "`meta'"

use "$work_data/add-china-data-metadata.dta", clear

merge 1:1 iso sixlet using "`meta'", nogenerate update

label data "Generated by distribute-national-income.do"
save "$work_data/distribute-national-income-metadata.dta", replace
