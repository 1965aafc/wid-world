// Start with the WID data
use "$work_data/add-china-data-output.dta", clear

keep if inlist(widcode, "mgdpro999i", "mnnfin999i")
drop p

reshape wide value, i(iso year) j(widcode) string
rename valuemgdpro999i gdp_lcu_wid
rename valuemnnfin999i nfi_lcu_wid

generate nfi_pct_wid = nfi_lcu_wid/gdp_lcu_wid

// Add other data sources
merge 1:1 iso year using "$work_data/imf-nfi.dta", ///
	nogenerate update assert(using master match)

// Net foreign income
generate nfi_pct = nfi_pct_wid
generate nfi_src = "Piketty and Zucman (2014)" if (nfi_pct_wid < .)
replace nfi_src = "Waldenstrom" if (nfi_pct_wid < .) & (iso == "SE")

replace nfi_src = "the IMF" if (nfi_pct >= .) & (nfi_pct_imf < .)
replace nfi_pct = nfi_pct_imf if (nfi_pct >= .) & (nfi_pct_imf < .)

keep iso year nfi_pct nfi_src

// Extrapolate 2015 value in 2016
reshape wide nfi_pct nfi_src, i(iso) j(year)
replace nfi_src2016 = "the value from the previous year as a % of GDP" if missing(nfi_pct2016) & !missing(nfi_pct2015)
replace nfi_pct2016 = nfi_pct2015 if missing(nfi_pct2016) & !missing(nfi_pct2015)
reshape long nfi_pct nfi_src, i(iso) j(year) string

drop if missing(nfi_pct)

label data "Generated by calculate-nfi.do"
save "$work_data/nfi.dta", replace
