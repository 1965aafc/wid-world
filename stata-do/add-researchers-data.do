
// This file adds all new data provided by researchers to WID, pre-cleaned in their respective folders.

// -----------------------------------------------------------------------------------------------------------------
// IMPORT ALL FILES

// China 2017 (PYZ2017)
use "$wid_dir/Country-Updates/China/2017/china-pyz2017.dta", clear

// US 2017 (PSZ2017, from Github folder)
append using "$us_data/PSZ/psz-distrib-nominal-2017.dta"

// Ivory Coast 2017 (Czajka2017)
append using "$wid_dir/Country-Updates/Ivory Coast/2017_July/ivory-coast-czajka2017.dta"

// UK 2017 (Alvaredo2017)
append using "$wid_dir/Country-Updates/UK/2017/August/uk-income-alvaredo2017.dta"

// Macro updates 2017 (Bauluz2017)
append using "$wid_dir/Country-Updates/Spain/2017/spain-bauluz2017.dta" // Spain
append using "$wid_dir/Country-Updates/Sweden/2017/sweden-bauluz2017.dta" // Sweden
append using "$wid_dir/Country-Updates/Japan/2017/August/macro-updates-bauluz2017.dta" // All others (Japan folder)

// Brazil 2017 (Morgan2017)
append using "$wid_dir/Country-Updates/Brazil/brazil-morgan2017.dta"

// India 2017 (Chancel2017)
append using "$wid_dir/Country-Updates/India/2017/August/india-chancel2017.dta"

// Germany 2017 (Bartels2017)
append using "$wid_dir/Country-Updates/Germany/2017/August/germany-bartels2017.dta"

// Russia 2017 (NPZ2017)
append using "$wid_dir/Country-Updates/Russia/2017/August/russia-npz2017.dta"

tempfile researchers
save "`researchers'"

// ----------------------------------------------------------------------------------------------------------------
// CREATE METADATA

// Remove some duplicates
drop if iso=="US" & substr(widcode,1,6)=="npopul" & author=="bauluz2017"

// Save metadata
generate sixlet = substr(widcode, 1, 6)
keep iso sixlet source method
order iso sixlet source method
duplicates drop

duplicates tag iso sixlet, gen(dup)
assert dup==0
drop dup

tempfile meta
save "`meta'"

// ----------------------------------------------------------------------------------------------------------------
// ADD DATA TO WID
use "$work_data/calculate-average-over-output.dta", clear
gen oldobs=1
append using "`researchers'"
replace oldobs=0 if oldobs!=1

// Drop old duplicated wid datas
duplicates tag iso year p widcode, gen(dup)
drop if dup & oldobs

// US 2017: drop specific widcodes
drop if (inlist(widcode,"ahweal992j","shweal992j","afainc992j","sfainc992j","aptinc992j") ///
	| inlist(widcode,"sptinc992j","adiinc992j","sdiinc992j","npopul992i","mhweal992j") ///
	| inlist(widcode,"mfainc992j","mfainc992j","mptinc992j","mdiinc992j","mnninc999i") ///
	| inlist(widcode,"mgdpro999i","mnnfin999i","inyixx999i","mconfc999i")) ///
	& (iso=="US") & (oldobs==1)

// Ivory Coast 2017: drop whole country
drop if iso=="CI" & oldobs

// UK 2017: drop duplicates
drop if iso=="UK" & oldobs & dup

// Macro updates 2017: drop all old series (widcode-years combinations), except for "n" and "i" where we fill gaps
gen id=iso+"_"+widcode
duplicates tag id, gen(dupvar)
qui levelsof id if dupvar>0 & author=="bauluz2017", local(todrop) clean
foreach i in `todrop'{
	qui drop if id=="`i'" & oldobs & !inlist(substr(widcode,1,1),"n","i")
}

// India 2017: drop duplicates and old fiscal income data
drop if substr(widcode, 2, 5)=="fiinc" & oldobs==1 & iso=="IN"

duplicates tag iso year p widcode, gen(duplicate)
assert duplicate==0

keep iso year p widcode currency value

label data "Generated by add-researchers-data.do"
save "$work_data/add-researchers-data-output.dta", replace

// ----------------------------------------------------------------------------------------------------------------
// ADD METADATA
use "$work_data/correct-wtid-metadata-output.dta", clear
merge 1:1 iso sixlet using "`meta'", nogenerate update replace

label data "Generated by add-researchers-data.do"
save "$work_data/add-researchers-data-metadata.dta", replace

