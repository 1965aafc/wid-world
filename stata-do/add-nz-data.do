import delimited using "$nz-data/NewZealand_update_2017_May.csv", clear delim(";") encoding("utf8")
		
		// Store source
		local source = v2[3]
		
		// Drop unnecessary information
		drop in 2/3
		drop in 4/7
		drop in 6
		
		// Macro to store variable information before reshape
		// (placeholder necessary for Stata not to remove quotes)
		local oldcode     "x"
		local note        "x"
		local unit        "x"
		local category    "x"
		local subcategory "x"
		local variable    "x"
		local subvariable "x"
		
		// List variables
		rename v1 year
		ds year, not
		local columns = r(varlist)
		local k = 1
		foreach col of local columns {
			forvalues i = 1/7 {
				local v`i' = `col'[`i']
			}
		
			local oldcode     `oldcode'     `"`v1'"'
			local note        `note'        `"`v2'"'
			local unit        `unit'        `"`v3'"'
			local category    `category'    `"`v4'"'
			local subcategory `subcategory' `"`v5'"'
			local variable    `variable'    `"`v6'"'
			local subvariable `subvariable' `"`v7'"'
			
			rename `col' value`k'
			local k = `k' + 1
		}
		// Remove the information stored in macros from the dataset
		drop in 1/7
		
		// Reshape
		cleanchars *, in(",") out(".")
		foreach var of varlist v*{
		cap replace `var' = subinstr(`var'," ","",.) 
		}
		destring *, replace
		drop if (year >= .) // Drop empty rows
		reshape long value, i(year) j(col)
		
		// Re-assign variable information
		generate source = `"`source'"'
		generate oldcode = ""
		generate note = ""
		generate unit = ""
		generate category = ""
		generate subcategory = ""
		generate variable = ""
		generate subvariable = ""
		quietly levelsof col, local(collist)
		foreach col of local collist {
			// Shift to ignore placeholder
			local colshift = `col' + 1
			
			local v1: word `colshift' of `oldcode'
			local v2: word `colshift' of `note'
			local v3: word `colshift' of `unit'
			local v4: word `colshift' of `category'
			local v5: word `colshift' of `subcategory'
			local v6: word `colshift' of `variable'
			local v7: word `colshift' of `subvariable'
			
			replace oldcode     = `"`v1'"' if (col == `col')
			replace note        = `"`v2'"' if (col == `col')
			replace unit        = `"`v3'"' if (col == `col')
			replace category    = `"`v4'"' if (col == `col')
			replace subcategory = `"`v5'"' if (col == `col')
			replace variable    = `"`v6'"' if (col == `col')
			replace subvariable = `"`v7'"' if (col == `col')
		}
		
		// Housekeeping
		drop col
		drop if value >= .
		drop if oldcode == ""
		
	display "done"
	
label data "Generated by add-nz-data.do"
save "$work_data/add-nz-data.dta", replace
