

// -----------------------------------------------------------------------------------------------------------------
// IMPORT ALL FILES

// US States 2017
use "$us_states_data/us-states-frank2017.dta", clear

// France inequality 2017 (GGP2017)
append using "$france_data/france-ggp2017.dta"

// UK wealth 2017 (Alvaredo2017)
append using "$uk_data/uk-wealth-alvaredo2017.dta"

// US inequality 2017 (PSZ2017)
append using "$wid_dir/Country-Updates/US/2017/September/PSZ2017-AppendixII.dta"

// Middle-East 2017 (Assouad2017)
append using "$wid_dir/Country-Updates/Middle-East/2017/October/middle-east-assouad2017.dta"
replace source = "[URL][URL_LINK]http://wid.world/document/" + ///
	"rethinking-lebanese-economic-miracle-extreme-concentration-income-wealth-" + ///
	"lebanon-2005-2014-wid-world-working-paper-201713/[/URL_LINK]" + ///
	"[URL_TEXT]Lydia Assouad (2017). Rethinking the Lebanese economic miracle: " + ///
	"The extreme concentration of income and wealth in Lebanon 2005-2014. " + ///
	"WID.world Working Paper 2017/13[/URL_TEXT][/URL]" if (author == "assouad2017" & iso == "LB")
replace source = "[URL][URL_LINK]http://wid.world/document/alvaredoassouadpiketty-middleeast-widworldwp201715/[/URL_LINK]" + ///
	"[URL_TEXT]Facundo Alvaredo; Lydia Assouad and Thomas Piketty (2017). " + ///
	"Measuring Inequality in the Middle East, 1990-2016: The Worldâ€™s Most Unequal Region? " + ///
	"WID.world Working Paper 2017/15[/URL_TEXT][/URL]" if (author == "assouad2017" & iso != "LB")

tempfile researchers
save "`researchers'"

// ----------------------------------------------------------------------------------------------------------------
// CREATE METADATA
generate sixlet = substr(widcode, 1, 6)
keep iso sixlet source method
order iso sixlet source method
duplicates drop
tempfile meta
save "`meta'"

// ----------------------------------------------------------------------------------------------------------------
// ADD DATA TO WID
use "$work_data/distribute-national-income-output.dta", clear
gen oldobs=1
append using "`researchers'"
replace oldobs=0 if oldobs!=1

// Drop old duplicated wid data
duplicates tag iso year p widcode, gen(dup)
drop if dup & oldobs==1

// France 2017: drop specific widcodes
drop if (inlist(widcode,"ahwbol992j","ahwbus992j","ahwcud992j","ahwdeb992j","ahweal992j") ///
	| inlist(widcode,"ahwequ992j","ahwfie992j","ahwfin992j","ahwfix992j","ahwhou992j") ///
	| inlist(widcode,"ahwnfa992j","ahwpen992j","bhweal992j","ohweal992j","shweal992j","thweal992j") ///
	| substr(widcode, 2, 2) == "fi") ///
	& (iso == "FR") & (oldobs==1)

// US inequality 2017: add only new widcode-years
preserve
keep if iso=="US"
duplicates drop year widcode oldobs, force // collapse to one line per year-widcode-oldobs
duplicates tag year widcode, gen(duplicate) // drop year-widcode duplicates in new data, to preserve old data
drop if duplicate & oldobs==0
drop if oldobs==1 // now drop all old observations
assert duplicate==0 // we have a list of new year-widcode data now
drop p oldobs duplicate value
gen toadd=1
tempfile newlist
save "`newlist'"
restore
merge m:1 year widcode using "`newlist'", nogen assert(master matched)
drop if toadd!=1 & oldobs==0 & iso=="US"

duplicates tag iso year p widcode, gen(duplicate)
assert duplicate==0

keep iso year p widcode currency value

label data "Generated by add-researchers-data-real.do"
save "$work_data/add-researchers-data-real-output.dta", replace


// ----------------------------------------------------------------------------------------------------------------
// ADD METADATA
use "$work_data/distribute-national-income-metadata.dta", clear
merge 1:1 iso sixlet using "`meta'", nogenerate update replace

label data "Generated by add-researchers-data-real.do"
save "$work_data/add-researchers-data-real-metadata.dta", replace


