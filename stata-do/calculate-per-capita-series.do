use "$work_data/calculate-wealth-income-ratio-output.dta", clear

// Data with adult population
keep if inlist(widcode, "npopul999i", "npopul992i")
reshape wide value, i(iso year p) j(widcode) string
keep iso year p valuenpopul992i valuenpopul999i
tempfile pop
save "`pop'"

// Calculate per capita series
use "$work_data/calculate-wealth-income-ratio-output.dta", clear

keep if substr(widcode, 1, 1) == "m"

// Drop Tobin's Q
drop if substr(widcode, 4, 3) == "toq"

merge n:1 iso year using "`pop'", nogenerate keep(match)

generate value999i = value/valuenpopul999i
generate value992i = value/valuenpopul992i

keep iso year widcode p currency value999i value992i

reshape long value, i(iso year p widcode) j(pop) string
replace widcode = "a" + substr(widcode, 2, 5) + pop

drop pop
drop if missing(value)

append using "$work_data/calculate-wealth-income-ratio-output.dta"

duplicates drop iso year p widcode, force


drop if inlist(widcode, "anninc999i", "anninc992i")
drop if inlist(widcode, "andpro999i", "andpro992i")
drop if inlist(widcode, "agdpro999i", "agdpro992i")
drop if inlist(widcode, "aconfc999i", "aconfc992i")
drop if inlist(widcode, "annfin999i", "annfin992i")
drop if inlist(widcode, "anweal999i", "anweal992i")
tempfile widdb
save "`widdb'"

keep if inlist(widcode, "mgdpro999i", "mndpro999i", "mnninc999i", "npopul999i", ///
	"npopul992i", "mconfc999i", "mnnfin999i", "mnweal999i")
drop currency

reshape wide value, i(iso year p) j(widcode) string

generate valueanninc999i = valuemnninc999i/valuenpopul999i
generate valueanninc992i = valuemnninc999i/valuenpopul992i

generate valueandpro999i = valuemndpro999i/valuenpopul999i
generate valueandpro992i = valuemndpro999i/valuenpopul992i

generate valueagdpro999i = valuemgdpro999i/valuenpopul999i
generate valueagdpro992i = valuemgdpro999i/valuenpopul992i

generate valueaconfc999i = valuemconfc999i/valuenpopul999i
generate valueaconfc992i = valuemconfc999i/valuenpopul992i

generate valueannfin999i = valuemnnfin999i/valuenpopul999i
generate valueannfin992i = valuemnnfin999i/valuenpopul992i

generate valueanweal999i = valuemnweal999i/valuenpopul999i
generate valueanweal992i = valuemnweal999i/valuenpopul992i

keep iso year p valuea*

reshape long value, i(iso year p) j(widcode) string

keep if value < .

// Keep old values if they already exist
append using "`widdb'"

// Sanity check: no duplicates
duplicates tag iso year p widcode, generate(duplicate)
assert duplicate == 0
drop duplicate

label data "Generated by calculate-per-capita-series.do"
save "$work_data/calculate-per-capita-series-output.dta", replace
