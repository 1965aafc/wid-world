import excel "$wb_data/global-economic-monitor/GDP at market prices, current LCU, millions, seas. adj..xlsx", ///
	clear allstring
sxpose, clear

ds _var1, not
foreach v of varlist `r(varlist)' {
	local year = `v'[1]
	rename `v' value`year'
}
drop in 1
destring value*, replace force
dropmiss, force

rename _var1 country

countrycode country, generate(iso) from("wb gem")
drop country

/*
// Identify problems in the data
replace value2015 = . if (iso == "AR")
replace value2015 = . if (iso == "IR")
*/

// Uzbekistan has problematic value for 2016
drop if iso=="UZ"

assert abs(value2015 - value2014)/value2015 < 0.5 if (value2015 < .)
assert abs(value2016 - value2015)/value2016 < 0.5 if (value2016 < .)

reshape long value, i(iso) j(year)
drop if value >= .
replace value = value*1e6
rename value gdp_lcu_gem

drop if year==$year

label data "Generated by import-wb-gem-gdp.do"
save "$work_data/wb-gem-gdp.dta", replace
