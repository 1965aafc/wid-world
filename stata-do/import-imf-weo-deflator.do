import delimited "$imf_data/world-economic-outlook/WEOApr2016all.csv", ///
	clear delimiter(";") varnames(1) rowrange(1:8405) encoding("utf8")

foreach v of varlist v10-v51 {
	local year: var label `v'
	if ("`year'" == "") {
		drop `v'
	}
	else {
		destring `v', replace force ignore(",")
		rename `v' def_imf`year'
	}
}

keep if weosubjectcode == "NGDP_D"
drop iso weocountrycode weosubjectcode subjectdescriptor subjectnotes units ///
	scale countryseriesspecificnotes

countrycode country, generate(iso) from("imf weo")
drop country

reshape long def_imf, i(iso) j(year)
drop if def_imf >= .
drop if year > 2015

rename def_imf def_weo

label data "Generated by import-imf-weo-deflator.do"
save "$work_data/imf-deflator-weo.dta", replace
