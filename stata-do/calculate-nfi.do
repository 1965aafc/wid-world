// Start with the WID data
use "$work_data/correct-widcodes-output.dta", clear

keep if inlist(widcode, "mgdpro999i", "mnnfin999i")
drop p

reshape wide value, i(iso year) j(widcode) string
rename valuemgdpro999i gdp_lcu_wid
rename valuemnnfin999i nfi_lcu_wid

generate nfi_pct_wid = nfi_lcu_wid/gdp_lcu_wid

// Add IMF NFI (% of GDP)
merge 1:1 iso year using "$work_data/imf-nfi.dta", ///
	nogenerate update assert(using master match)

// Add OECD NFI (% of GDP)
merge 1:1 iso year using "$oecd_data/nfi/oecd-nfi.dta", ///
	 update nogen assert(using master match)

*tw con nfi_pct_oecd nfi_pct_imf year if iso=="IE"

// Compute net foreign income, with priority WID, then fill gaps with source (IMF/OECD) with largest coverage
generate nfi_pct = nfi_pct_wid
generate nfi_src = "Piketty and Zucman (2014)" if (nfi_pct_wid < .)
replace nfi_src = "Waldenstrom" if (nfi_pct_wid < .) & (iso == "SE")

sort iso year
bys iso: egen n_imf=nvals(year) if !mi(nfi_pct_imf)
bys iso: egen n_oecd=nvals(year) if !mi(nfi_pct_oecd)
replace n_imf=0 if n_imf==.
replace n_oecd=0 if n_oecd==.

replace nfi_src = "the IMF" if mi(nfi_pct) & !mi(nfi_pct_imf) & n_imf>n_oecd
replace nfi_pct = nfi_pct_imf if mi(nfi_pct) & !mi(nfi_pct_imf) & n_imf>n_oecd

replace nfi_src = "the OECD" if mi(nfi_pct) & !mi(nfi_pct_oecd) & n_imf<=n_oecd
replace nfi_pct = nfi_pct_oecd if mi(nfi_pct) & !mi(nfi_pct_oecd) & n_imf<=n_oecd

keep iso year nfi_pct nfi_src

/*
// Plot conflicting sources (type "q" in command line to plot next graph, "BREAK" to stop)
bys iso: egen num=nvals(nfi_src)
levelsof iso if num>1 & !mi(num), local(countries)
pause on
foreach c in `countries'{
	tw (con nfi_pct year if iso=="`c'" & !inlist(nfi_src,"the IMF","the OECD")) ///
		(con nfi_pct year if iso=="`c'" & nfi_src=="the IMF") ///
		(con nfi_pct year if iso=="`c'" & nfi_src=="the OECD"), ///
		t("`c'") legend(label(1 "WID") label(2 "IMF") label(3 "OECD"))
	pause
}
drop num
*/

// Extrapolate (pastyear - 1) value in pastyear
reshape wide nfi_pct nfi_src, i(iso) j(year)
local prepastyear = $pastyear - 1
di `prepastyear'
replace nfi_src$pastyear = "the value from the previous year as a % of GDP" if missing(nfi_pct$pastyear) & !missing(nfi_pct`prepastyear')
replace nfi_pct$pastyear = nfi_pct`prepastyear' if missing(nfi_pct$pastyear) & !missing(nfi_pct`prepastyear')

// Keep constant back to 1950 when missing
local prepastyear = $pastyear - 1
forval i = `prepastyear' (-1) 1950{
	local next=`i'+1
	replace nfi_src`i'="the value from the next year as a % of GDP" if mi(nfi_pct`i')
	replace nfi_pct`i'=nfi_pct`next' if mi(nfi_pct`i') 
}

reshape long nfi_pct nfi_src, i(iso) j(year) string

drop if missing(nfi_pct)

destring year, replace force

label data "Generated by calculate-nfi.do"
save "$work_data/nfi.dta", replace


