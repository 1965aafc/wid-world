// -----------------------------------------------------------------------------------------------------------------
// IMPORT ALL FILES

// US States 2017
use "$us_states_data/us-states-frank2017.dta", clear

// France inequality 2017 (GGP2017)
append using "$france_data/france-ggp2017.dta"

// UK wealth 2017 (Alvaredo2017)
append using "$uk_data/uk-wealth-alvaredo2017.dta"

// US inequality 2017 (PSZ2017)
append using "$wid_dir/Country-Updates/US/2017/September/PSZ2017-AppendixII.dta"

// Middle-East 2017 (Assouad2017)
append using "$wid_dir/Country-Updates/Middle-East/2017/October/middle-east-assouad2017.dta"

tempfile researchers
save "`researchers'"

// ----------------------------------------------------------------------------------------------------------------
// CREATE METADATA
generate sixlet = substr(widcode, 1, 6)
keep iso sixlet source method
order iso sixlet source method
duplicates drop
replace method = " " if method == ""
tempfile meta
save "`meta'"

// ----------------------------------------------------------------------------------------------------------------
// ADD DATA TO WID
use "$work_data/distribute-national-income-output.dta", clear
gen oldobs=1
append using "`researchers'"
replace oldobs=0 if oldobs!=1

// France 2017: drop specific widcodes
drop if (inlist(widcode,"ahwbol992j","ahwbus992j","ahwcud992j","ahwdeb992j","ahweal992j") ///
	| inlist(widcode,"ahwequ992j","ahwfie992j","ahwfin992j","ahwfix992j","ahwhou992j") ///
	| inlist(widcode,"ahwnfa992j","ahwpen992j","bhweal992j","ohweal992j","shweal992j","thweal992j") ///
	| substr(widcode, 2, 2) == "fi") ///
	& (iso == "FR") & (oldobs==1)

// US inequality (PSZ 2017 Appendix II): drop g-percentiles (DINA imported before), drop new duplicated wid data
drop if (iso=="US") & (oldobs==0) & (length(p)-length(subinstr(p,"p","",.))==1) & (p!="pall")
duplicates tag iso year p widcode, gen(dupus)
drop if dupus & oldobs==0 & iso=="US"

// Drop old duplicated wid data
duplicates tag iso year p widcode, gen(dup)
drop if dup & oldobs==1

duplicates tag iso year p widcode, gen(duplicate)
assert duplicate==0

keep iso year p widcode currency value

label data "Generated by add-researchers-data-real.do"
save "$work_data/add-researchers-data-real-output.dta", replace


// ----------------------------------------------------------------------------------------------------------------
// ADD METADATA
use "$work_data/distribute-national-income-metadata.dta", clear
merge 1:1 iso sixlet using "`meta'", nogenerate update replace
replace method = "" if method == " "
replace method = "" if iso == "FR" & substr(sixlet, 2, 5) == "ptinc"

label data "Generated by add-researchers-data-real.do"
save "$work_data/add-researchers-data-real-metadata.dta", replace


