
import excel "$wid_dir/Country-Updates/France/2017/August/France_WID.world.xlsx", clear

// Clean
renvars, map(strtoname(@[3]))
drop if _n<4
destring _all, replace
rename WID_code year
dropmiss, force

// Change units
foreach var of varlist m*{
replace `var'=`var'*1000000000
}
replace mcwtoq999i=mcwtoq999i/1000000000 // Tobin Q not to be multiplied

// Reshape
ds year, not
renvars `r(varlist)', pref(value)
reshape long value, i(year) j(widcode) string
drop if mi(value)
gen iso="FR"
generate currency = "EUR" if inlist(substr(widcode, 1, 1), "a", "t", "m", "i")
gen p="pall"

levelsof widcode, local(levels)

tempfile francemacro
save "`francemacro'"

// Create metadata
generate sixlet = substr(widcode, 1, 6)
keep iso sixlet
duplicates drop
generate source = `"[URL][URL_LINK][/URL_LINK]"' ///
	+ `"[URL_TEXT]Garbinti, Goupille and Piketty (2016); [/URL_TEXT][/URL]; "'
generate method = ""
tempfile meta
save "`meta'"

// Add data to WID
use "$work_data/add-swedish-data-output.dta", clear
gen oldobs=1
append using "`francemacro'"
duplicates tag iso year p widcode, gen(dup)
qui count if dup==1 & iso!="FR"
assert r(N)==0
drop if oldobs==1 & dup==1
drop oldobs dup

label data "Generated by add-france-macro-data.do"
save "$work_data/add-france-macro-data-output.dta", replace

// Add metadata
use "$work_data/add-swedish-data-metadata.dta", clear
merge 1:1 iso sixlet using "`meta'", nogenerate update replace

label data "Generated by add-france-macro-data.do"
save "$work_data/add-france-macro-data-metadata.dta", replace
