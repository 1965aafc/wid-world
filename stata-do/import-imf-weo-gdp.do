import delimited "$imf_data/world-economic-outlook/WEOApr2016all.csv", ///
	clear delimiter(";") varnames(1) rowrange(1:8405) encoding("utf8")

foreach v of varlist v10-v51 {
	local year: var label `v'
	if ("`year'" == "") {
		drop `v'
	}
	else {
		destring `v', replace force ignore(",")
		rename `v' value`year'
	}
}

keep if weosubjectcode == "NGDP"
drop iso weocountrycode weosubjectcode subjectdescriptor subjectnotes units ///
	scale countryseriesspecificnotes

countrycode country, generate(iso) from("imf weo")
drop country

reshape long value, i(iso) j(year)
drop if value >= .

drop if year > 2015

replace value = value*1e9
rename value gdp_lcu_weo

label data "Generated by import-imf-weo-gdp.do"
save "$work_data/imf-weo-gdp.dta", replace
