use "$work_data/clean-up-output.dta", clear

// Keep thresholds and top averages only
keep if (substr(widcode, 1, 1) == "a" & regexm(p, "^p([0-9\.]+)p100$")) | ///
	(substr(widcode, 1, 1) == "t" & regexm(p, "^p([0-9\.]+)p100$"))

// Split widcode
generate onelet = substr(widcode, 1, 1)
generate vartype = substr(widcode, 2, .)

// Get percentile
generate long perc = round(1e3*real(regexs(1))) if (regexm(p, "^p([0-9\.]+)(p100)?$"))
drop p widcode

reshape wide value, i(iso year perc vartype) j(onelet) string

// Calculate Pareto coefficient
generate value = valuea/valuet
drop if missing(value)
drop valuea valuet

generate widcode = "b" + vartype
drop vartype

generate p = "p" + string(perc/1e3) + "p100"
drop perc

tempfile bp
save "`bp'"

// Complete the metadata: first, retrieve the metadata for average fiscal income
generate sixlet = "a" + substr(widcode, 2, 5)
keep iso sixlet
duplicates drop
drop if (iso == "FR")

merge 1:1 iso sixlet using "$work_data/add-researchers-data-real-metadata.dta", nogenerate keep(match)

replace method = "Calculated by WID.world as the ratio of top average over corresponding threshold."
replace sixlet = "b" + substr(sixlet, 2, 5)

append using "$work_data/add-researchers-data-real-metadata.dta"
label data "Generated by calculate-pareto-coef.do"
save "$work_data/calculate-pareto-coef-output-metadata.dta", replace

// Add to the data
use "$work_data/clean-up-output.dta", clear

drop if substr(widcode, 1, 1) == "b"

append using "`bp'"

label data "Generated by calculate-pareto-coef.do"
save "$work_data/calculate-pareto-coef-output.dta", replace
